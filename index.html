<!DOCTYPE html>
<html lang="en">
  <head>
    <meta charset="UTF-8" />
    <meta http-equiv="X-UA-Compatible" content="IE=edge" />
    <meta name="viewport" content="width=device-width, initial-scale=1.0" />
    <title>Listing 7-7, Lighting with spot light.</title>
    <script src="./app.js" defer></script>
    <script src="./glMatrix.js" defer></script>
    <script src="./webgl-debug.js" defer></script>
    <script src="./webgl-utils.js" defer></script>

    <script id="shader-vs" type="x-shader/x-vertex">
      attribute vec3 aVertexPosition;
      attribute vec3 aVertexNormal;
      attribute vec2 aTextureCoordinates;

      uniform mat4 uMVMatrix;
      uniform mat4 uPMatrix;
      uniform mat3 uNMatrix;

      varying vec2 vTextureCoordinates;
      varying vec3 vNormalEye;
      varying vec3 vPositionEye3;

      void main() {
        vec4 vertexPositionEye4 = uMVMatrix * vec4(aVertexPosition, 1.0);
        vPositionEye3 = vertexPositionEye4.xyz / vertexPositionEye4.w;

        vNormalEye = normalize(uNMatrix * aVertexNormal);

        gl_Position = uPMatrix * uMVMatrix * vec4(aVertexPosition, 1.0);
        vTextureCoordinates = aTextureCoordinates;
      }
    </script>

    <script id="shader-fs" type="x-shader/x-fragment">
      precision mediump float;

      varying vec2 vTextureCoordinates;
      varying vec3 vNormalEye;
      varying vec3 vPositionEye3;

      uniform vec3 uAmbientLightColor;
      uniform vec3 uDiffuseLightColor;
      uniform vec3 uSpecularLightColor;
      uniform vec3 uLightPosition;
      uniform vec3 uSpotDirection;

      uniform sampler2D uSampler;

      const float shininess = 32.0;
      const float spotExponent = 40.0;
      const float spotCosCutoff = 0.97;

      vec3 lightWeighting = vec3(0.0, 0.0, 0.0);

      void main() {
        vec3 vectorToLightSource = normalize(uLightPosition - vPositionEye3);

        float diffuseLightWeighting = max(dot(vNormalEye, vectorToLightSource), 0.0);

        if (diffuseLightWeighting > 0.0) {
          float spotEffect = dot(normalize(uSpotDirection), normalize(-vectorToLightSource));

          if (spotEffect > spotCosCutoff) {
            spotEffect = pow(spotEffect, spotExponent);

            vec3 reflectionVector = normalize(reflect(-vectorToLightSource, vNormalEye));

            vec3 viewVectorEye = -normalize(vPositionEye3);

            float rdotv = max(dot(reflectionVector, viewVectorEye), 0.0);

            float specularLightWeighting = pow(rdotv, shininess);

            lightWeighting =
              spotEffect * uDiffuseLightColor * diffuseLightWeighting +
              spotEffect * uSpecularLightColor * specularLightWeighting;
          }
        }

        lightWeighting += uAmbientLightColor;

        vec4 texelColor = texture2D(uSampler, vTextureCoordinates);
        gl_FragColor = vec4(lightWeighting.rgb * texelColor.rgb, texelColor.a);
      }
    </script>
  </head>
  <body onload="startup();">
    <canvas id="myGLCanvas" width="500" height="500"></canvas>
  </body>
</html>

<!-- 
  7-7 예제에서는 애니메이션이 필요없기 때문에,
  1초마다 fps를 계산해주지 않아도 됨.

  따라서, fps값을 표시해주는 div 요소도 제거해버림.
-->

<!-- 
  7-5 예제 버텍스 셰이더와 차이점


  이전 예제에서의 버텍스 셰이더와 전부 동일한 코드.

  7-7 예제에서도 프래그먼트 셰이더에서 조명계산을 수행하는
  '퐁 셰이딩(per-fragment shading)' 기법을 사용할 것이기 때문에

  프래그먼트 세이더에서 조명 계산을 할 때 필요한 varying 변수들
  (vTextureCoordinates, vNormalEye, vPositionEye3)만 계산해서 넘겨주고,
  gl_Position에 버텍스 좌표 데이터를 클립 좌표로 변환하여 할당하는 역할만 하면 됨.
-->

<!-- 
  7-6 예제 프래그먼트 셰이더와 차이점


  이전 예제에서의 프래그먼트 셰이더는 '점 광원'을 기준으로 조명값을 계산했음.
  그러나, 7-7 예제는 '스포트 광원'을 기준으로 조명값을 계산해야 하므로,
  몇몇 코드가 추가되었음.



  일단 새롭게 추가된 프래그먼트 셰이더의 변수들을 정리해보자.


  -uniform vec3 uSpotDirection;

  얘는 스포트 광원이 방출되는 방향, 즉 원뿔의 중심 축을 가리키는 벡터이다.
  이 값은 셰이더 내에서 별도로 계산되는 것은 아니고, 
  app.js의 setupLights() 함수에서 임의의 값([0.0, -1.0, 0.0])을 지정하여 전송해 줌.
  spotlight가 바닥을 향해(음의 y축 방향) 내리꽂기 때문에, y좌표값만 -1.0으로 넣어준 듯 함.


  -const float spotExponent = 40.0;

  얘는 spotEffect(원뿔에서 빛 감소량)을 계산할 때 사용하는 거듭제곱 횟수(지수)를 의미함.
  마치 스펙큘러 성분을 구할 때, 반사 벡터와 관찰자 벡터를 내적한 뒤, 
  결과값을 shininess(광택값)으로 거듭제곱하는 것과 비슷한 역할을 함.
  spotDirection과 광원벡터를 내적한 값을 거듭제곱할 때 사용되는 값이라고 보면 됨. 


  -const float spotCosCutoff = 0.97;

  얘는 스포트 광원에서 spotDirection(원뿔의 중심축)과 원뿔의 가장 겉부분의 lightDirection이
  이루는 최대 각도(?) 정도라고 보면 됨.

  p.359의 설명을 참고하자면, '원뿔을 양분하는 각도' 라고도 표현함.

  근데 셰이더 코드에서는 정확히 말하면, '각도값'이라기 보다는,
  계산의 편의상 각도의 '코사인값'을 지정해놓았다고 보면 됨.
  책에서는 0.97을 14도의 코사인값에 해당한다고 설명하고 있음.

  저 각도값보다 클 경우, 0.97보다 작은 코사인값을 갖게 될 것이고,
  이 말은 lightDirection이 원뿔 영역을 벗어난 상태이기 때문에,
  '원뿔의 감소량(spotEffect)'를 사용한 조명계산에서 제외됨.

  즉, 코사인값이 1 ~ 0.97 사이일 경우에만 원뿔의 감소량을 사용한 조명계산을 할 수 있는 것.


  -float spotEffect

  이 값은 셰이더 코드 내에서의 위치에 따라서 의미가 좀 달라짐.

  일단, 바깥쪽 if block에서 계산되는 spotEffect는
  spotDirection 벡터와 lightDirection 벡터를 각각 정규화한 뒤,
  두 벡터를 내적한 코사인값을 결과값으로 할당받게 됨.
  그러나 이 계산으로부터 얻은 결과값(코사인값)을 '원뿔에서의 빛 감소량' 이라고 할 수는 없음.
  따라서 이 때의 spotEffect는 두 벡터의 사잇각의 코사인값 정도로 생각하면 됨.

  또한, 안쪽 if block에서 계산되는 spotEffect는
  원래의 값을 spotExponent 만큼 거듭제곱한 결과값을 리턴받기 때문에
  p.359에서 설명한 공식에 맞게 '원뿔에서의 빛 감소량'으로 사용할 수 있는 값이 됨.
-->

<!-- 
  프래그먼트 셰이더에서 각 if block의 조건문에 대한 설명


  -첫 번째 조건문 diffuseLightWeighting > 0.0

  얘는 디퓨즈 성분을 계산할 때, 노멀 벡터와 광원 벡터를 내적해준 값이지?
  즉, 노멀 벡터와 광원 벡터 사잇각의 코사인값이 0.0보다 커야한다는 뜻이고,
  이 말은, 두 벡터의 사잇각이 90도보다는 작아야 한다는 뜻이지.

  90도이거나 90도보다 크다는 것은 어떤 의미일까?
  이 말은 광원벡터가 해당 버텍스에 수평인 방향으로 옆쪽으로 스쳐가거나,
  버텍스 표면의 내부에서 광원벡터가 나온다는 얘기가 됨.
  -> 원뿔 모양의 스포트 광원에서 이런 방향의 광원벡터가 나올 리 없겠지!

  따라서 이런 경우에는 디퓨즈 성분, 스펙큘러 성분, 원뿔 빛 감소량을 계산하지 않고,
  총 밝기값인 lightWeighting에도 포함시키지 않도록 하겠다는 것임.
  

  -두 번째 조건문 spotEffect > spotCosCutoff
  
  이 조건문에서 사용된 spotEffect는 spotDirection 벡터와 lightDirection 벡터의 사잇각의 코사인값이고,
  spotCosCutoff는 두 벡터의 사잇각의 코사인값의 최소치를 의미함.

  즉, 이 예제에서 사용할 스포트 광원의 원뿔의 각도는 
  spotCosCutoff 코사인값을 각도로 표현한 값(여기서는 14도)이 최대치이다 라는 뜻이지.

  lightDirection이 원뿔 각도의 최대치를 넘어서면 p.359에서도 설명했듯이
  조명계산에서 제외시켜야 하기 때문에, 조건문으로 필터링을 했다고 보면 됨.
-->

<!-- 
  앰비언트 성분은 if block 밖에서 따로 합산해 준 이유


  1. 첫 번째 이유는, spotEffect(원뿔에서의 빛 감소량)에 영향을 받는 성분은 
  디퓨즈 성분과 스펙큘러 성분이다. 
  따라서 두 성분만 if block 내부에서 spotEffect를 곱해줘서 먼저 합산한 것이다.
  
  2. 두 번째 이유는, 광원벡터가 원뿔의 범위를 벗어나는 버텍스 또는 프래그먼트의 경우,
  앰비언트 성분값도 넣어주지 않는다면, 해당 부위가 아예 새카맣게 보이기 때문에
  비사실적이고 어색해 보이게 된다. 반사광이 반영되지 않았기 때문이다. 
  Phong Reflection Model은 반사광을 추적하여 계산하지 못하기 때문에,
  '반사광을 흉내내는 값'이 필요한데, 이 값이 바로 앰비언트 성분이라고 보면 된다.
  이에 관한 자세한 내용은 p.325 참고
-->

<!-- 
  float spotEffect = dot(normalize(uSpotDirection), normalize(-vectorToLightSource));
  에서 내적을 할 때, 왜 -vectorToLightSource를 중복해서 정규화하는 것일까?

  값을 붙여서 벡터의 방향을 바꾸는 이유는,
  (광원 -> 버텍스/프래그먼트) 로 방향을 전환하기 위해서 한 것이지만.
  이미 프래그먼트 셰이더 상단에서 정규화를 미리 해줬는데 
  중복해서 정규화하는 이유는 무엇일까?

  정확한 이유를 알 수는 없지만,
  아마 벡터의 방향을 전환하면 정규화를 다시 해줘야 하는 듯 함.

  vec3 reflectionVector = normalize(reflect(-vectorToLightSource, vNormalEye));
  이 코드도 보면, reflect 함수에 방향을 바꾼 광원벡터를 넣어서 반사벡터를 얻지만,
  그렇게 결과값으로 얻은 반사벡터도 다시 정규화해주고 있으니까...


  또한, 다른 내적계산과 다르게 max() 내장함수로 최저값을 0.0으로 제한하지 않는 이유는
  스포트 광원은 원뿔 모양으로 빛을 방출하기 때문에 
  lightDirection과 spotDirection의 내적 결과값이 0.0보다 작아질 수 없음. (= 90도보다 커질 수 없음.)
-->
